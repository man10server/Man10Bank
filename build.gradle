plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'red.man10'
version = '1.0'

def kotlinVersion = '2.0.20'
def coroutinesVersion = '1.9.0'
def junitVersion = '5.11.0'
def ktorVersion = '2.3.12'
def serializationVersion = '1.6.3'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    // Server-provided APIs
    compileOnly 'io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT'
    compileOnly ("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: 'org.bukkit', module: 'bukkit'
    }
    // Kotlin stdlib (shade to jar)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    // Coroutines (for lightweight async work)
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"

    // WebAPI クライアント（Ktor）
    implementation "io.ktor:ktor-client-core:${ktorVersion}"
    implementation "io.ktor:ktor-client-cio:${ktorVersion}"
    implementation "io.ktor:ktor-client-content-negotiation:${ktorVersion}"
    implementation "io.ktor:ktor-serialization-kotlinx-json:${ktorVersion}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${serializationVersion}"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:${kotlinVersion}"
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.9.0'
    testImplementation "com.github.MilkBowl:VaultAPI:1.7"
    testImplementation("ch.qos.logback:logback-classic:1.4.14")
    testImplementation "io.ktor:ktor-client-mock:${ktorVersion}"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}

shadowJar {
    archiveClassifier.set('')
}

build.dependsOn shadowJar

test {
    useJUnitPlatform()
}

// KotlinとJavaのツールチェーンをJDK 21に統一
kotlin {
    jvmToolchain(21)
}

// Kotlinファイルを src/main/java 配下も対象にする
sourceSets {
    main {
        kotlin.srcDirs += 'src/main/java'
    }
    test {
        kotlin.srcDirs += 'src/test/kotlin'
    }
}